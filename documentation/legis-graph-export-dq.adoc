= Legis-graph - Exploring US Congress as A Graph

== Exporting data from Neo4j

image::https://raw.githubusercontent.com/legis-graph/legis-graph/master/img/datamodel.png[float=right]

We will start with one of the queries that analised the contributions by party for a certain occupation.

Because we want to export the data for external analysis, instead of running the aggregates in Neo4j, we will export all individual donations as records with the relevant fields.

We are flattening our graph into a file, exactly the opposite of what we did when we loaded it.

[source,cypher]
----
MATCH (o:Occupation) WHERE o.name CONTAINS "MARKETING"
MATCH (o)-[:HAS_OCCUPATION]-(con:Contributor)-[:MADE_CONTRIBUTION]->(cont:Contribution)-[:MADE_TO]->(com:FECCommittee)-[:FUNDS]->(cand:Legislator)
RETURN con.name AS contributorName, cont.amount AS contributionAmount, com.name AS committeeName, cand.firstName + ' '  + cand.lastName AS candidateName, cand.party AS candidateParty
----

Export the results to a file by clicking on the download icon on the top right corner of the results box.

Your file is ready to be loaded by Tableau, QlikSense, Excel, your favourite BI/viz/analytics tool


== Exercises

image::https://raw.githubusercontent.com/legis-graph/legis-graph/master/img/datamodel.png[float=right]

* Extend the previous query by adding geographic info to the exported file. Suggestion: Tableau likes zipcodes and we can get zipcodes from the FEC committees (cm.txt) data file.
* Build a map visualization of the contributions by party by state/city



== Data Quality 

Zip codes in the committee dataset are not perfect. There are different levels of granularity.

Can you identify in the graph the ones that the visualization tool does not understand?

[source,cypher]
----
MATCH (cont:Contribution)-[:MADE_TO]->(com:FECCommittee)-[:FUNDS]->(cand:Legislator)
RETURN distinct com.zip
----


How can we try to make all consistent? Truncating the string? Using regular expressions?

We may want to update the value of the date field or we can leave it as is and just apply the correction on export

== Exercise: 

* Apply the zip code correction to the export query. You can truncate the zip codes using the substring function.

[source,cypher]
----
MATCH ...
RETURN ... , truncate(...) AS zip
----


* Create a new field on Committee nodes called zip5 containing the 5 digit version of the zip code

[source,cypher]
----
MATCH ... (com:Committee) ...
SET com.zip5 = truncate(...) 
----

The previous could also be done using regular expressions if the modification was more complicated ( Matching on WHERE with _=~_ and updating with procedure/function: _apoc.text.replace(<original>,<expr>,<repl>)_    )


== Using time

Contributions have a date but the format needs to be modified to be usable by Tableau. We need to transform dates like '10072015' into '10/07/2015'

We are going to fix it using a combination of substring function and apoc.text.join

Again, we can update the value of the date field or we can leave it as is and just apply the format on export

[source,cypher]
----
apoc.text.join([substring(cont.date...), ... ],"/") 
----

And now we are going to produce a time series representation of the donations in Tableau


== Advanced analysis for more complex problems

The 'Renaissance Tech problem' 

[source,cypher]
----
MATCH (n:Employer) 
WHERE n.name CONTAINS 'RENAISSANCE TECH'
RETURN n.name AS name ORDER BY name
----
What can we do about it?

One idea: some word (token) analysis. We are going to look at the words that form an employer name.

But before we do that let's create an index to improve lookup performance.


[source,cypher]
----
CREATE INDEX ON :EmployerToken(name)
----

And now we tokenize the company names

[source,cypher]
----
MATCH (o:Employer) 
UNWIND split(o.name, ' ') AS word
WITH o, word 
MERGE (t:EmployerToken {name : replace(replace(word,'.',''),',','')})
CREATE (o)-[:HAS_TOKEN]->(t)
----

Look what we've done:

[source,cypher]
----
MATCH  (e1)-[:HAS_TOKEN]-(t1:EmployerToken)-[:HAS_TOKEN]-(e2)-[:HAS_TOKEN]-(t2:EmployerToken)-[:HAS_TOKEN]-(e3) 
RETURN * limit 10
----

Now we can ask queries about how similar company names are:
[source,cypher]
----
//Token Analysis
MATCH (t:EmployerToken) WHERE t.name = 'RENAISSANCE'
MATCH (emp1)-[:HAS_TOKEN]->(t)<-[:HAS_TOKEN]-(emp2) WHERE emp1.name < emp2.name
WITH emp1.name AS employer1, 
     size((emp1)-[:HAS_TOKEN]->()) AS emp1tokencount,  
     emp2.name AS employer2, 
     size((emp2)-[:HAS_TOKEN]->()) AS emp2tokencount, 
     size((emp1)-[:HAS_TOKEN]->()<-[:HAS_TOKEN]-(emp2)) as overlap  
//DEFINE YOUR THRESHOLD
WHERE  toFloat(overlap) > ((emp1tokencount + emp2tokencount) / 4) 
return employer1, emp1tokencount, employer2, emp2tokencount, 
       ((emp1tokencount + emp2tokencount) / 4)  as threshold,
       overlap order by overlap DESC limit 100
----

And from here, decide what do do: do we link the nodes? do we normalize names into a 'canonical' version? 
