= Congressional Graph Algorithms

== Network Analysis With Neo4j Graph Algorithms

image::img/lg_datamodel.png[float=right]

* Centralities
* Clustering
* Triadic closures and inferred relationships
*
* Global graph algorithms with APOC


//== US Congress As A Graph
//
//image::{img}/lg_datamodel.png[float=right]
//
//=== Datamodel
//
//=== Import
//
//You can import this dataset into Neo4j using http://www.lyonwj.com/LazyWebCypher/?file=https://raw.githubusercontent.com/legis-graph/legis-graph/master/quickstart/114/legis_graph_import_114.cypher[this cypher script] or for a more in-depth data import walkthrough with
//
//* pass:a[<a play-topic='http://guides.neo4j.com/legisgraphimport.html'>this in-depth browser guide</a>]
//
// TODO: some exploratory queries
//
//=== Exploratory Queries
//
//.The meta model
//[source,cypher,subs=attributes]
//----
//CALL apoc.meta.graph()
//----
//
//.California legislators
//[source,cypher,subs=attributes]
//----
//MATCH (l:Legislator)-[:REPRESENTS]->(s:State {code: "CA"})
//MATCH (p:Party)<--(l)-->(c:Body)
//RETURN *
//----
//

== Bill Cosponsorships

Let's start by using bill cosponsorships for our analysis. A cosponorship occurs any time two legislators sponsor the same bill.

.Cosponsors for a single Bill
[source,cypher,subs=attributes]
----
MATCH (b:Bill) WITH b LIMIT 1
MATCH (l:Legislator)<-[:SPONSORED_BY]-(b)
RETURN *
----

Bill cosponsorship is a specific example of the concept of *inferred relationships* or *triadic closures*. In this case we are inferring a relationship between two legislators who sponsor the same bill. 

image::img/bill_cosponsors.png[]


=== Persisting Inferred Relationships

.Persist all COSPONSORED relationships
[source,cypher,subs=attributes]
----
// Persist inferred COSPONSORED relationships
MATCH (l1:Legislator)<-[:SPONSORED_BY]-(b:Bill)-[:SPONSORED_BY]->(l2:Legislator)
WHERE id(l1) < id(l2)
WITH l1, l2, COUNT(*) AS weight
CREATE (l1)-[r:COSPONSORED]->(l2)
SET r.weight = weight
----

== Centrality measures

In graph theory and network analysis, indicators of centrality identify the most important vertices within a graph. Applications include identifying the most influential person(s) in a social network, key infrastructure nodes in the Internet or urban networks, and super-spreaders of disease.

=== Degree Centrality

Degree centrality is the number of relationships connected to a specific node. In the context of this network, it is the number of `COSPONSORED` relationships.

[source,cypher]
----
MATCH (l:Legislator)
RETURN l.firstName + " " + l.lastName AS legislator, size ((l)<-[:COSPONSORED]-()) AS degree ORDER BY degree DESC LIMIT 25
----

=== Weighted Degree Centrality

[source,cypher]
----
MATCH (l:Legislator)-[r:COSPONSORED]-()
RETURN l.firstName + " " + l.lastName AS legislator, sum(r.weight) AS weightedDegree ORDER BY weightedDegree DESC LIMIT 25
----

== Apoc Procedures

image::img/apoc_proc.png[float=right]

=== User Defined Procedures

User defined procedures are written in Java, deployed to the database and callable from Cypher.

=== https://github.com/neo4j-contrib/neo4j-apoc-procedures[Apoc library]

Library of procedures for many common Neo4j tasks, including graph algorithsm, data import, refactoring, indexing, system monitoring, ...


== Betweenness Centrality

The betweenness centrality of a node in a network is the number of shortest paths between two other members in the network on which a given node appears.

Betweenness centality is an important metric because it can be used to identify “brokers of information” in the network or nodes that connect disparate clusters.

image::img/betweenness-centrality.png[]

.The red nodes have a high betweenness centrality and are connectors of clusters.

[source,cypher]
----
MATCH (l:Legislator)
WITH collect(l) AS legislators
CALL apoc.algo.betweenness(['COSPONSORED'], legislators, 'OUTGOING') YIELD node, score
SET node.betweenness = score
RETURN node.firstName + " " + node.lastName AS legislator, score ORDER BY score DESC LIMIT 25
----


== Closeness Centrality

Nodes with high closeness centality are often highly connected within clusters in the graph, but not necessarily highly connected outside of the cluster.

Closeness centrality is the inverse of the average distance to all other characters in the network.

image::img/closeness-centrality.png[]

.Nodes with high closeness centrality are connected to many other nodes in a network.
[source,cypher]
----
MATCH (l:Legislator)
WITH collect(l) AS legislators
CALL apoc.algo.closeness(['COSPONSORED'], legislators, 'OUTGOING') YIELD node, score
RETURN node.firstName + " " + node.lastName AS legislator, score ORDER BY score DESC LIMIT 25
----


== PageRank

image::img/page-rank.png[]

.The size of each node is proportional to the size and number of nodes with an outgoing relationship to it.

[source,cypher]
----
MATCH (l:Legislator) WITH collect(l) AS ls
CALL apoc.algo.pageRank(ls) YIELD node, score
RETURN node.firstName + " " + node.lastName AS legislator, score ORDER BY score DESC LIMIT 10
----

== PageRank - Inferred Relationships

// TODO: insert image from wikipedia

=== Inferred Relationships and Political Influence

The main sponsor of a bill can be said to have demonstrated political influence over cosponsors. We can find these inferred `INFLUENCED` relationships in the graph with this query:

.Find inferred INFLUENCED relationships
[source,cypher]
----
MATCH (b:Bill)-[r:SPONSORED_BY]->(sponsor:Legislator)
WHERE r.cosponsor = False
MATCH (b)-[s:SPONSORED_BY]->(cosponsor:Legislator)
WHERE s.cosponsor = True
RETURN id(sponsor) AS source, id(cosponsor) AS target, count(*) AS weight ORDER BY weight DESC LIMIT 25
----


=== PageRank On Inferred Relationships

We can run PageRank using inferred relationships without actually persisting these to the graph:

//So let's remove all the `COSPONSORED` relatiionships:
//
//[source,cypher]
//----
//MATCH ()-[r:COSPONSORED]-()
//DELETE r
//----.

.Run PageRank on inferred INFLUENCED relationships (that are not persisted in the graph)
[source,cypher]
----
CALL apoc.algo.pageRankWithCypher({iterations:20, write:true, node_cypher: 'MATCH (l:Legislator) RETURN id(l) AS id', rel_cypher:'MATCH (b:Bill)-[r:SPONSORED_BY]->(sponsor:Legislator)
WHERE r.cosponsor = False
MATCH (b)-[s:SPONSORED_BY]->(cosponsor:Legislator)
WHERE s.cosponsor = True
RETURN id(sponsor) AS source, id(cosponsor) AS target, count(*) AS weight ORDER BY weight DESC'})
----


== Most influential Senator with influence over certain topics

[source,cypher]
----
MATCH (b:Body {type: "Senate"})<-[:ELECTED_TO]-(l:Legislator)<-[:SPONSORED_BY]-(:Bill)-[d:DEALS_WITH]->(s:Subject)
WHERE s.title CONTAINS "Technology"
RETURN l, COUNT(*) AS num ORDER BY l.pagerank DESC LIMIT 10
----


//== Community Detection
//
//image::img/community-1.png[]
//
//.Community detection algorithms seek to minimize **modularity**, the ratio of the number of relationships across clusters to thhe number of relationships within cluster.
//
//== Community Detection In Congress
//
//First we'll need to filter on only members of the Senate or House, since we won't see bill sponsorhips across bodies.
//
//[source,cypher]
//----
//MATCH (l:Legislator) WHERE l.type = "House"
//SET l:Rep
//----
//
//We can run the label propogation community detection algorithm using APOC:
//
//[source,cypher]
//----
//CALL apoc.algo.community(40,['Rep'],'partition','COSPONSORED','OUTGOING','weight',10000)
//----
//
//Now let's see what clusters we've found:
//
//[source,cypher]
//----
//MATCH (l:Rep) WHERE exists(l.partition)
//RETURN l.partition, COUNT(*) AS num
//ORDER BY num DESC
//----
//
//How do these clusters break across party lines?
//
//[source,cypher]
//----
MATCH (l:Rep)
//RETURN l.partition, l.party, COUNT(*) AS num
//----
//
//== Visualization
//
//image::img/community_viz.png[]
//
//* Node size determined by betweenness centrality `:Legislator(betweenness)`
//* Color determined by cluster `:Legislator(partitiion)`
//* Relationship thickness determined by cospnosor weight
//
//
//== Visualization
//
//image::http://www.lyonwj.com/public/img/graph-of-thrones.png[]
//
//Generated with https://github.com/johnymontana/neovis.js[neovis.js]

//== Clustering

