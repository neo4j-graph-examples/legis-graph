= Data Load

== Creating nodes

You want to create a node in your graph for every record in your file. The keyword in Cypher is 'CREATE'

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
CREATE (s:State { code: row.CMTE_ST })
----

Check what the previous load has produced... something unexpected?


== Understanding how data load works (2)

Duplicates! We want a single node representing a state so we can then link all cities in the state to it...
Let's delete the states we have created and start again

[source,cypher]
----
MATCH (s:State) DELETE s
----


The keyword in Cypher is MERGE (semantics: CREATE if it does not exist yet, MATCH if it is already there)

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
MERGE (s:State { code: row.CMTE_ST })
----

The same query with MERGE instead of CREATE should have done the job.
Now let's do the same with the cities in the sixth column. The same record may contain multiple entities.

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
MERGE (s:State { code: row.CMTE_ST })
MERGE (c:City { name: row.CMTE_CITY })
----

And finally, we are ready to connect cities with states, we just need to add the named relationship

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
MERGE (s:State { code: row.CMTE_ST })
MERGE (c:City { name: row.CMTE_CITY })
MERGE (c)-[:LOCATED_IN]->(s)
----

Now you can remove the limit to run the script on the whole dataset
And you can have a look at the entities you've just created using the same graph pattern

[source,cypher,subs=attributes]
----
MATCH (c:City)-[:LOCATED_IN]->(s:State)
RETURN c,s LIMIT 15
----

== Understanding how data load works: Exercises

* Using the same file (cm.txt) try to create nodes representing committees identified by the committee ID (`CMTE_ID`) and treasurers identified by their name (`TRES_NM`) and connect them to indicate that the committee has this person as treasurer.

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
RETURN row.CMTE_ID AS committeId, row.TRES_NM AS treasurerName
----

* Extend your load script with more attributes of the committe using the keyword SET (Cypher refcard)

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
...
MERGE (c:Committee ...
SET c.name = row[...]
----

(watch out for unexpected problems when run on the whole data set, raw data is never perfect)

* Using the ccl.txt file now, load legislators and link them to the committees that fund them.
